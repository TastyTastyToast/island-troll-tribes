library MotionSensor requires ID, TimerUtils, IsTypeThing, PublicLibrary {

  constant real PING_RATE = 5;
  constant real NUM_PINGS = 5;

  struct PingData {
    player player;

    private integer numTimesPinged = 0;

    static method create( player p ) -> thistype {
      thistype self = thistype.allocate();
      self.player = p;
      return self;
    }

    private method onDestroy() {
      player = null;
    }

    method increment() -> thistype {
      numTimesPinged += 1;
      return this;
    }

    method done() -> boolean {
      return numTimesPinged == NUM_PINGS;
    }
  }

  function EnemiesFilter() -> boolean {
    return GetUnitState( GetFilterUnit(), UNIT_STATE_LIFE ) >= 0;
  }

  function Ping( player p ) {
    unit u;
    unit scout = GetPlayersTroll( p );
    group g = CreateGroup();
    real range;

    // Improved Motion Sensor
    if ( UnitHasBuffBJ( scout, BUFF_MAP_MAGIC ) ) {
      debug BJDebugMsg( "Casting Improved Motion Sensor!" );
      range = 5000.00 + 1000.00 * I2R( GetUnitAbilityLevel( scout, SPELL_SCOUT_MOTION_SENSOR_R ) ); 
    }
    // Motion Sensor
    else {
      debug BJDebugMsg( "Casting Motion Sensor!" );
      range = 2000.00 + 1000.00 * I2R( GetUnitAbilityLevel( scout, SPELL_SCOUT_MOTION_SENSOR_R ) ); 
    }

    GroupEnumUnitsInRangeOfLoc( g, GetUnitLoc( scout ), range, Condition( function EnemiesFilter ) );

    u = FirstOfGroup( g );    
    while ( u != null && IsUnitEnemy( u, p ) ) {
      debug BJDebugMsg( "we're in hurrr" );
      debug BJDebugMsg( "unit is a " + ID2S( GetUnitTypeId( u ) ) );

      // Blue Attack ping for Invisible units
      if ( IsUnitTroll( u ) && GetUnitAbilityLevel( u, BUFF_THIEF_CLOAK ) > 0 ) {
          PingMinimapEx( GetUnitX( u ), GetUnitY( u ), PING_RATE, 0, 0, 255, false );
      }
      // Red Attack ping for trolls
      else if ( IsUnitTroll( u ) ) {
          PingMinimapEx( GetUnitX( u ), GetUnitY( u ), PING_RATE, 255, 0, 0, false );
      }
      // Green Attack ping for Boats
      else if ( GetUnitTypeId( u ) == UNIT_TROLL_TRANSPORT_SHIP ) {
          PingMinimapEx( GetUnitX( u ), GetUnitY( u ), PING_RATE, 0, 255, 0, false );
      }
      GroupRemoveUnit( g, u );
      u = FirstOfGroup( g );
    }

    DestroyGroup( g );
    g = null;
    u = null;
    scout = null; // prevent leaks
  }

  function MotionSensor() {
    unit scout = GetTriggerUnit();
    PingData p;

    p = PingData.create( GetOwningPlayer( GetTriggerUnit() ) );
    TimerStart( NewTimerEx( p ), PING_RATE, true, function() {
      timer t = GetExpiredTimer();
      PingData p = PingData( GetTimerData( t ) );
      if ( p.done() ) {
        p.destroy();
        ReleaseTimer( t );
      } else {
        SetTimerData( t, p.increment() );
        Ping( p.player );
      }
      t = null;
    });

    scout = null;
  }

  function onInit() {
    trigger t = CreateTrigger( );
    TriggerRegisterAnyUnitEventBJ( t, EVENT_PLAYER_UNIT_SPELL_ENDCAST );
    TriggerAddCondition( t, Condition( function () -> boolean {
      return ( GetSpellAbilityId() == SPELL_SCOUT_MOTION_SENSOR_R );
    }));
    TriggerAddAction( t, function MotionSensor );
  }
}
